
title: start
position: 25,-239
---
///name of the player 
<<declare $playerName = "a">>
<<declare $pronoun1 = "they">>
<<declare $pronoun2 = "them">>
<<declare $guide = "???">>
<<declare $combatDemoCleared = false >>
<<PlayerNameSet EventSystem>>
<<PlayerPronounSet EventSystem>>
<<set $askedName to false>>
//Start of dialogue 
// ---------------------------------------------Terry Quests ------------------
Player: Hello! 
Guide: Welcome adventurer!
<<jump introQuestions>>
=== 
title: introQuestions
position: 25,-40
---
<<TutorialOptions EventSystem>>
<<if $askedWhere == false>>
-> Where am I? 
    Guide: The Glittersphere!
    <<set $askedWhere to true>>
-> Who are you? 
    <<jump whoAreYou>>
<<endif>>
<<if $askedWhere && $Glitter == false>>
-> Glittersphere?
    <<jump GlittersphereIntro>>
-> Who are you?
    <<jump whoAreYou>>
-> What should I do?
    <<jump WhatShouldIDoIntro>>
-> Bye
<<endif>>
<<if $Glitter && $askedName == false>>
-> Who are you?
    <<jump whoAreYou>>
-> What should I do?
    <<jump WhatShouldIDoIntro>>
-> Bye
<<endif>>
<<if $Glitter && $askedName>>
-> What should I do?
    <<jump WhatShouldIDoIntro>>
-> Bye
<<endif>>
=== 
title: whoAreYou
position: -271,268
---
<<if $askedName>>
    <<jump AlreadyAskedName>> // check if the player has already asked this before route to another node.
<<endif>> 
<<GuideNameSet EventSystem>>
<<set $askedName to true>>
Guide: Oh sorry, my name is Terry.
Guide: What's yours?
Player: I'm {$playerName}.
Guide: Nice to meet you {$playerName}.
 <<jump introQuestions>>
===
title: AlreadyAskedName
position: -272,455
---
Guide: I'm Terry & you're {$playerName}.
Player: I just wanted to double check.
 <<jump introQuestions>>
===
title: GlittersphereIntro
position: -18,272
---
<<set $Glitter to true>>
Guide: It's what we named this place.
-> How long ago was that?
    Guide: Hmm... I'm not sure.
    Guide: Time gets a bit fuzzy.
    <<jump introQuestions>>
-> We?
    Guide: There's lots of us around.
    Guide: More arriving every day.
    <<jump introQuestions>>
===
title: WhatShouldIDoIntro
position: 430,301
---
Guide: Lots of folks need help.
Guide: Bobbin is in a tangle.
Guide: Minky is a hazard to himself.
Guide: The golden scissors..
-> Bobbin?
    Guide: Head right, you'll find them. 
-> Minky?
    Guide: Just don't touch him.
    Player: Why not? 
    Guide: It's safer that way.
    Player: Hmm okay? 
-> Golden Scissors? 
    Guide: Yeah...
    Player: Go on... 
    Guide: They sort of went missing.
    Player: Missing?
    Guide: We have looked everywhere.
    -> Maybe I can help?
        Guide: You are welcome to look for them.
    -> They were golden? 
        Guide: Gold. Big. Can't miss them.  
    Guide: There is a reward for them.
    Player: What kind of reward? 
    Guide: It's a mystery.
    Guide: Likely something good though! 
    Player: Okay, thanks! 
<<Checkpoint1Cleared EventSystem>>
<<jump introQuestions>>
===
title: bobbin
position: 582,-227
---
// ---------------------------------------------Bobbin Quests ------------------
<<if $BobbinQuest && $StitchChat>>
<<jump BobbinStitchComplete>>
<<endif>>
<<if $BobbinQuest && $StitchChat == false>>
<<jump BobbinUpdate>>
<<endif>>
Bobbin: I'm fretting over split ends!
Player: Hello?
Bobbin: H-hi!
-> Are you Bobbin?
    Bobbin: Yes! Who told you?
    Bobbin: Was it Stitch?
    Player: No, who is Stitch?
-> Are you Minky? 
    Bobbin: No, I'm Bobbin. 
    Bobbin: But I need your help. 
    Player: What do you need help with? 
Bobbin: Stitch wants to cut things off. 
Player: ... 
Bobbin: Stitch is my partner.
Bobbin: We tied the knot a while ago. 
Bobbin: But it's all a tangle now. 
Player: Did something happen? 
Bobbin: I'm not sure. 
Bobbin: C-could you ask her for me? 
-> Sure, seems easy. 
-> Is there a reward? 
    Bobbin: Oh... there can be?
    Bobbin: I'll think of something. 
Bobbin: Thanks!
Bobbin: Oh, what was your name?
Player: I'm {$playerName}.
Bobbin: Thanks again, {$playerName}. 
<<set $BobbinQuest to true>>
===
title: BobbinUpdate
---
Bobbin: H-Have you talked to her? 
Player: Not yet. 
Bobbin: Okay. Let me know when you do. 
===
title: stitch
position: 331,-227
---
<<if $Mended>>
    <<jump DistractedBobbin>>
<<elseif $StitchChat>>
    Stitch: I'll catch up. 
    Player: Okay, I'll head to Bobbin.
<<elseif $BobbinQuest && $StitchChat == false>>
    Player: Hello!
    Stitch: ...
    Player: Are you Stitch?
    Stitch: Yeah? 
    Player: Are you mad at Bobbin?
    <<jump StitchDetails>>
<<elseif $BobbinQuest == false>>
    Player: Hello!
    Stitch: What?
    -> Are you okay? 
        Stitch: Leave me alone. 
        Player: Bye? 
        Stitch: ...
    -> I'm {$playerName}. 
        Stitch: ...
        Player: And you are?
        Stitch: ...
<<endif>>
===
title: StitchDetails
position: 331,-227
---
Stitch: I'm not mad. 
Player: You aren't?
Stitch: I'm sad.
-> Sad?
    Stitch: Yeah about Bobbin. 
-> Bobbin might be sad too. 
    Stitch: They are?
Player: Bobbin seemed worried. 
Stitch: Worried about what? 
Player: I think you two should talk. 
Stitch: ...
Stitch: You're right. 
Stitch: You go ahead, I'll catch up. 
Player: Okay. I'll see you over there. 
<<set $StitchChat to true>>
===
title: BobbinStitchComplete
position: 331,-227
---
<<if $Mended>>
<<jump DistractedBobbin>>
<<endif>>
Bobbin: H-have you talked to her?
<<QuestBobbin EventSystem>> //Move stitch to closer location. 
Bobbin: Stitch! 
Stitch: Hey Bobbin.
Player: ...
Stitch: I-
Bobbin: Before you do it 
Bobbin: I'm sorry
Stitch: Sorry? 
Bobbin: Yeah.
Stitch: I'm sorry too.
Player: ...
Bobbin: Sorry, {$playerName}.
Player: It's all good. 
Player: You have stuff to talk through.
Stitch: I'm glad {$pronoun1} said something.
Bobbin: Me too. 
<<Checkpoint2Cleared EventSystem>>
<<set $Mended to true>>
===
title: DistractedBobbin
---
Bobbin: Really?
Stitch: Mmhm. 
Bobbin: Go on!!
Stitch: Well...
===
title: cali
position: 331,-227
---
// ---------------------------------------------Cali Combat Quests ------------------
Cali: Help!
FluffBug: GARRRRR!
Cali: Rip it apart! 
-> I'll protect you!
    <<CombatDemo EventSystem>>
-> Maybe later.
===
title: cali2
position: 519,167
---
Cali2: Thanks for saving me! 
Player: Of course.
-> Golden scissors?
    Cali2: I haven't seen them in a while.
    Player: So not around here?
    Cali2: Nah, maybe keep heading right. 
    Player: Thank you! 
    Cali2: Good luck adventurer! 
-> Bye! 
===
title: Minky
position: 331,-227
---
// ---------------------------------------------Minky ------------------
Player: Hello! 
Minky: Hey, finger guns.
<<if $BobbinQuest>>
-> Are you Stitch?
    Minky: Nah name's Minky. 
    Minky: What's yours?
    Player: I'm {$playerName}.
    Minky: Nice to meet you {$playerName}!
    Minky: Stitch was over by a tree. 
    Player: Okay thanks. 
-> Did you just say finger guns?
    <<jump MinkyFingerGuns>> 
-> You doing okay?
    <<jump MinkyOkay>>
<<endif>>
<<if $BobbinQuest == false>>
-> Did you just say finger guns?
    <<jump MinkyFingerGuns>> 
-> You doing okay?
    <<jump MinkyOkay>>
<<endif>>
===
title: MinkyFingerGuns
---
Minky: Yeah, I'd do them but
Minky: can't really move my hands.
Player: Can I help?
Minky: No thanks. 
===
title: MinkyOkay
position: 389,43
---
Minky: Yeah, little pinned.
-> Why are you pinned?
    Minky: Got finger gun happy. 
    Player: So you got pinned?
    Minky: Yeah. They were too deadly. 
    Player: Your finger guns?
    Minky: Yeah.
-> Do you need help?
    Minky: I'm good.
    Player: You sure?
    Minky: Yeah.
    Minky: Going to think about things. 
    Player: Want to talk about it?
    Minky: Maybe another time. 
===
title: Twill
position: 331,-227
---
// ---------------------------------------------Twill Optional Quests ------------------
<<if $WhiteBadge>>
<<jump TwillSolved>>
<<endif>>
Twill: Fight my fluffbug!
BonusBug: GARRRRRS!
-> Let's fight!
    <<Combat1 EventSystem>>
-> Let's talk this out. 
    Twill: Oh.
    <<Quest2Complete EventSystem>>
    Player: What's wrong?
    Twill: I just wanted to fight.
    Player: Because?
    Twill: Cause I'm scary!
    Player: Are you?
    Twill: Yes.
    Player: ... 
    Twill: Maybe.
    Player: ...
    Twill: I have to be so I don't get hurt.
    Player: Not everyone is going to hurt you. 
    Twill: But many people have. 
    Player: I'm sorry to hear that.
    Player: I won't hurt you. 
    Twill: Really? 
    Player: Just get rid of that fluffbug.
    Twill: Okay... 
    Player: It's a deal then.
    Twill: Sure. 
    Twill: Take this white heart badge.
    Player: For what?
    Twill: My way of saying thanks.
    Player: I'll wear it with pride. 
    <<set $WhiteBadge to true>>
-> No thanks! 
===
title: TwillSolved
---
Twill: Thanks for being nice. 
Player: Of course, take care! 
===
title: Twill2
---
Twill2: ...
Player: Not so scary now.
Twill2: Enjoy that bloody heart badge.
Player: Thanks, I will. 
Twill2: ... 
===
title: backStitch
position: 331,-227
---
// ---------------------------------------------Demo Complete------------------
Player: Hello!
BackStitch: Hey.
BackStitch: This is the end of the demo.
-> I'm done with the demo. 
    <<EndDemo EventSystem>>
-> What about the golden scissors? 
    BackStitch: The adventure continues...
    BackStitch: in the full version of the game! 
    -> I'm done with the demo. 
        <<EndDemo EventSystem>>
    -> I want to go back. 
-> I want to go back.
===
title: fluffBug
position: 389,-29
---
Player: Hi. 
FluffBug: GARRRRR!
-> Let's Fight!
    <<CombatDemo EventSystem>>
-> Nevermind. 
===